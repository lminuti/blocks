unit Blocks.Tests.JSON;

interface

uses
  System.Classes,
  System.SysUtils,
  System.JSON,
  System.Generics.Collections,
  DUnitX.TestFramework,

  Blocks.Core,
  Blocks.JSON;

type
  TMyTestObj = class(TObject)
  private
    FStrProp: string;
    FIntProp: Integer;
  public
    property StrProp: string read FStrProp write FStrProp;
    property IntProp: Integer read FIntProp write FIntProp;
  end;

  // ----- string list -----
  TTaggedObj = class(TObject)
  private
    FName: string;
    FTags: TStringList;
  public
    constructor Create;
    destructor Destroy; override;

    property Name: string read FName write FName;
    property Tags: TStringList read FTags;
  end;

  // ----- classic string list -----

  TFlagObj = class(TObject)
  private
    FName: string;
    FFlags: TStringList;
  public
    constructor Create;
    destructor Destroy; override;

    property Name: string read FName write FName;
    property Flags: TStringList read FFlags;
  end;

  // ----- object list -----

  TItemObj = class(TObject)
  private
    FValue: string;
    FCount: Integer;
  public
    property Value: string read FValue write FValue;
    property Count: Integer read FCount write FCount;
  end;

  TItemList = class(TObjectList<TItemObj>)
  public
    constructor Create;
  end;

  TListObj = class(TObject)
  private
    FTitle: string;
    FItems: TItemList;
  public
    constructor Create;
    destructor Destroy; override;

    property Title: string read FTitle write FTitle;
    property Items: TItemList read FItems;
  end;

  // ----- string dictionary -----

  TStrDict = class(TDictionary<string, string>)
  end;

  TStrDictObj = class(TObject)
  private
    FName: string;
    FProps: TStrDict;
  public
    constructor Create;
    destructor Destroy; override;

    property Name: string read FName write FName;
    property Props: TStrDict read FProps;
  end;

  // ----- object dictionary -----

  TItemDict = class(TObjectDictionary<string, TItemObj>)
  public
    constructor Create;
  end;

  TObjDictObj = class(TObject)
  private
    FName: string;
    FChildren: TItemDict;
  public
    constructor Create;
    destructor Destroy; override;

    property Name: string read FName write FName;
    property Children: TItemDict read FChildren;
  end;

  TDynObj = class
  private
    FJSON: TJSONValue;
    function GetName: string;
  public
    function ToJSON: TJSONValue;
    procedure FromJSON(AJSON: TJSONValue);

    property Name: string read GetName;

    constructor Create;
    destructor Destroy; override;
  end;

  TDynObjEnvelop = class
  private
    FDynObj: TDynObj;
  public
    property DynObj: TDynObj read FDynObj write FDynObj;
    constructor Create;
    destructor Destroy; override;
  end;

  [TestFixture]
  TJSONTest = class(TObject)
  private
    FMyTestObj: TMyTestObj;
  public
    [Setup]
    procedure Setup;
    [TearDown]
    procedure TearDown;

    [Test]
    procedure TestSerialization_BasicProperties;
    [Test]
    procedure TestDeserialization_BasicProperties;
    [Test]
    procedure TestSerialization_StringList;
    [Test]
    procedure TestDeserialization_StringList;
    [Test]
    procedure TestSerialization_ClassicStringList;
    [Test]
    procedure TestDeserialization_ClassicStringList;
    [Test]
    procedure TestSerialization_ObjectList;
    [Test]
    procedure TestDeserialization_ObjectList;
    [Test]
    procedure TestSerialization_StringDictionary;
    [Test]
    procedure TestDeserialization_StringDictionary;
    [Test]
    procedure TestSerialization_ObjectDictionary;
    [Test]
    procedure TestSerialization_DynamicObject;
    [Test]
    procedure TestDeserialization_DynamicObject;
    [Test]
    procedure TestDeserialization_ObjectDictionary;
    [Test]
    procedure TestDeserialization_StringList_Empty;
    [Test]
    procedure TestDeserialization_ClassicStringList_Empty;
    [Test]
    procedure TestDeserialization_ObjectList_Empty;
    [Test]
    procedure TestDeserialization_StringDictionary_Empty;
    [Test]
    procedure TestDeserialization_ObjectDictionary_Empty;
    [Test]
    procedure TestDeserialization_StringList_Missing;
    [Test]
    procedure TestDeserialization_ClassicStringList_Missing;
    [Test]
    procedure TestDeserialization_ObjectList_Missing;
    [Test]
    procedure TestDeserialization_StringDictionary_Missing;
    [Test]
    procedure TestDeserialization_ObjectDictionary_Missing;
    [Test]
    procedure TestJSONObject_PreservesKeyOrderOnParse;
    [Test]
    procedure TestJSONObject_PreservesKeyOrderOnRoundTrip;
  end;

const
  MyTestJSON =
    '''
    {
      "strProp": "TestValue",
      "intProp": 43
    }
    ''';

  TaggedObjJSON =
    '''
    {
      "name": "TestName",
      "tags": ["alpha", "beta", "gamma"]
    }
    ''';

  FlaggedObjJSON =
    '''
    {
      "name": "TestName",
      "flags": ["alpha", "beta", "gamma"]
    }
    ''';

  ListObjJSON =
    '''
    {
      "title": "MyList",
      "items": [
        { "value": "first", "count": 1 },
        { "value": "second", "count": 2 }
      ]
    }
    ''';

  StrDictObjJSON =
    '''
    {
      "name": "TestLabel",
      "props": {
        "key1": "value1",
        "key2": "value2"
      }
    }
    ''';

  ObjDictObjJSON =
    '''
    {
      "name": "DictLabel",
      "children": {
        "child1": { "value": "v1", "count": 10 },
        "child2": { "value": "v2", "count": 20 }
      }
    }
    ''';

  TaggedObjEmptyJSON =
    '''
    {
      "name": "TestName",
      "tags": []
    }
    ''';

  FlaggedObjEmptyJSON =
    '''
    {
      "name": "TestName",
      "flags": []
    }
    ''';

  ListObjEmptyJSON =
    '''
    {
      "title": "MyList",
      "items": []
    }
    ''';

  StrDictObjEmptyJSON =
    '''
    {
      "name": "TestLabel",
      "props": {}
    }
    ''';

  ObjDictObjEmptyJSON =
    '''
    {
      "name": "DictLabel",
      "children": {}
    }
    ''';

  TaggedObjMissingJSON =
    '''
    {
      "name": "TestName"
    }
    ''';

  FlaggedObjMissingJSON =
    '''
    {
      "name": "TestName"
    }
    ''';

  ListObjMissingJSON =
    '''
    {
      "title": "MyList"
    }
    ''';

  StrDictObjMissingJSON =
    '''
    {
      "name": "TestLabel"
    }
    ''';

  ObjDictObjMissingJSON =
    '''
    {
      "name": "DictLabel"
    }
    ''';

  // Keys deliberately not in alphabetical order, to prove insertion/parse
  // order is what is preserved (not a sort).
  OrderedKeysJSON =
    '''
    {
      "zebra": "1",
      "alpha": "2",
      "mike": "3",
      "bravo": "4",
      "delta": "5"
    }
    ''';

implementation

{ TTaggedObj }

constructor TTaggedObj.Create;
begin
  inherited Create;
  FTags := TStringList.Create;
end;

destructor TTaggedObj.Destroy;
begin
  FTags.Free;
  inherited;
end;

{ TItemList }

constructor TItemList.Create;
begin
  inherited Create(True);
end;

{ TListObj }

constructor TListObj.Create;
begin
  inherited Create;
  FItems := TItemList.Create;
end;

destructor TListObj.Destroy;
begin
  FItems.Free;
  inherited;
end;

{ TStrDictObj }

constructor TStrDictObj.Create;
begin
  inherited Create;
  FProps := TStrDict.Create;
end;

destructor TStrDictObj.Destroy;
begin
  FProps.Free;
  inherited;
end;

{ TItemDict }

constructor TItemDict.Create;
begin
  inherited Create([doOwnsValues]);
end;

{ TObjDictObj }

constructor TObjDictObj.Create;
begin
  inherited Create;
  FChildren := TItemDict.Create;
end;

destructor TObjDictObj.Destroy;
begin
  FChildren.Free;
  inherited;
end;

{ TJSONTest }

procedure TJSONTest.Setup;
begin
  FMyTestObj := TMyTestObj.Create;
end;

procedure TJSONTest.TearDown;
begin
  FMyTestObj.Free;
end;

procedure TJSONTest.TestSerialization_BasicProperties;
begin
  FMyTestObj.StrProp := 'MyValue';
  FMyTestObj.IntProp := 42;
  var LJSON := TJsonHelper.ObjectToJSON(FMyTestObj);
  try
    Assert.AreEqual('MyValue', LJSON.GetValue<string>('strProp'), 'strProp');
    Assert.AreEqual(42, LJSON.GetValue<Integer>('intProp'), 'intProp');
  finally
    LJSON.Free;
  end;
end;

procedure TJSONTest.TestSerialization_ClassicStringList;
begin
  var LObj := TFlagObj.Create;
  try
    LObj.Name := 'TestName';
    LObj.Flags.Add('alpha');
    LObj.Flags.Add('beta');
    LObj.Flags.Add('gamma');
    var LJSON := TJsonHelper.ObjectToJSON(LObj);
    try
      Assert.AreEqual('TestName', LJSON.GetValue<string>('name'), 'name');
      var LTags := LJSON.FindValue('flags') as TJSONArray;
      Assert.IsNotNull(LTags, 'flags array present');
      Assert.AreEqual(3, LTags.Count, 'flags count');
      Assert.AreEqual('alpha', LTags.Items[0].Value, 'flags[0]');
      Assert.AreEqual('beta', LTags.Items[1].Value, 'flags[1]');
      Assert.AreEqual('gamma', LTags.Items[2].Value, 'flags[2]');
    finally
      LJSON.Free;
    end;
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestSerialization_DynamicObject;
begin
  var LTempJSON := TJSONObject.ParseJSONValue('{"name": "luca"}');
  try
    var LObj := TDynObjEnvelop.Create;
    try
      LObj.DynObj.FromJSON(LTempJSON);
      var LJSON := TJsonHelper.ObjectToJSON(LObj);
      try
        var LDynObjJSON := LJSON.GetValue<TJSONObject>('dynObj');
        Assert.IsNotNull(LDynObjJSON, 'dynObj is null');
        Assert.AreEqual('luca', LDynObjJSON.GetValue<string>('name'), 'Property "name" not found');
      finally
        LJSON.Free;
      end;
    finally
      LObj.Free;
    end;
  finally
    LTempJSON.Free;
  end;
end;

procedure TJSONTest.TestDeserialization_BasicProperties;
begin
  var LObj := TJsonHelper.JSONToObject<TMyTestObj>(MyTestJSON);
  try
    Assert.AreEqual('TestValue', LObj.StrProp, 'strProp');
    Assert.AreEqual(43, LObj.IntProp, 'intProp');
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestDeserialization_ClassicStringList;
begin
  var LObj := TJsonHelper.JSONToObject<TFlagObj>(FlaggedObjJSON);
  try
    Assert.AreEqual('TestName', LObj.Name, 'name');
    Assert.AreEqual(3, LObj.Flags.Count, 'tags count');
    Assert.AreEqual('alpha', LObj.Flags[0], 'tags[0]');
    Assert.AreEqual('beta', LObj.Flags[1], 'tags[1]');
    Assert.AreEqual('gamma', LObj.Flags[2], 'tags[2]');
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestSerialization_StringList;
begin
  var LObj := TTaggedObj.Create;
  try
    LObj.Name := 'TestName';
    LObj.Tags.Add('alpha');
    LObj.Tags.Add('beta');
    LObj.Tags.Add('gamma');
    var LJSON := TJsonHelper.ObjectToJSON(LObj);
    try
      Assert.AreEqual('TestName', LJSON.GetValue<string>('name'), 'name');
      var LTags := LJSON.FindValue('tags') as TJSONArray;
      Assert.IsNotNull(LTags, 'tags array present');
      Assert.AreEqual(3, LTags.Count, 'tags count');
      Assert.AreEqual('alpha', LTags.Items[0].Value, 'tags[0]');
      Assert.AreEqual('beta', LTags.Items[1].Value, 'tags[1]');
      Assert.AreEqual('gamma', LTags.Items[2].Value, 'tags[2]');
    finally
      LJSON.Free;
    end;
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestDeserialization_StringList;
begin
  var LObj := TJsonHelper.JSONToObject<TTaggedObj>(TaggedObjJSON);
  try
    Assert.AreEqual('TestName', LObj.Name, 'name');
    Assert.AreEqual(3, LObj.Tags.Count, 'tags count');
    Assert.AreEqual('alpha', LObj.Tags[0], 'tags[0]');
    Assert.AreEqual('beta', LObj.Tags[1], 'tags[1]');
    Assert.AreEqual('gamma', LObj.Tags[2], 'tags[2]');
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestSerialization_ObjectList;
begin
  var LObj := TListObj.Create;
  try
    LObj.Title := 'MyList';
    var LItem1 := TItemObj.Create;
    LItem1.Value := 'first';
    LItem1.Count := 1;
    LObj.Items.Add(LItem1);
    var LItem2 := TItemObj.Create;
    LItem2.Value := 'second';
    LItem2.Count := 2;
    LObj.Items.Add(LItem2);
    var LJSON := TJsonHelper.ObjectToJSON(LObj);
    try
      Assert.AreEqual('MyList', LJSON.GetValue<string>('title'), 'title');
      var LItems := LJSON.FindValue('items') as TJSONArray;
      Assert.IsNotNull(LItems, 'items array present');
      Assert.AreEqual(2, LItems.Count, 'items count');
      Assert.AreEqual('first', LItems.Items[0].GetValue<string>('value'), 'items[0].value');
      Assert.AreEqual(1, LItems.Items[0].GetValue<Integer>('count'), 'items[0].count');
      Assert.AreEqual('second', LItems.Items[1].GetValue<string>('value'), 'items[1].value');
      Assert.AreEqual(2, LItems.Items[1].GetValue<Integer>('count'), 'items[1].count');
    finally
      LJSON.Free;
    end;
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestDeserialization_ObjectList;
begin
  var LObj := TJsonHelper.JSONToObject<TListObj>(ListObjJSON);
  try
    Assert.AreEqual('MyList', LObj.Title, 'title');
    Assert.AreEqual(2, LObj.Items.Count, 'items count');
    Assert.AreEqual('first', LObj.Items[0].Value, 'items[0].value');
    Assert.AreEqual(1, LObj.Items[0].Count, 'items[0].count');
    Assert.AreEqual('second', LObj.Items[1].Value, 'items[1].value');
    Assert.AreEqual(2, LObj.Items[1].Count, 'items[1].count');
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestSerialization_StringDictionary;
begin
  var LObj := TStrDictObj.Create;
  try
    LObj.Name := 'TestLabel';
    LObj.Props.Add('key1', 'value1');
    LObj.Props.Add('key2', 'value2');
    var LJSON := TJsonHelper.ObjectToJSON(LObj);
    try
      Assert.AreEqual('TestLabel', LJSON.GetValue<string>('name'), 'name');
      var LProps := LJSON.FindValue('props') as TJSONObject;
      Assert.IsNotNull(LProps, 'props object present');
      Assert.AreEqual('value1', LProps.GetValue<string>('key1'), 'props.key1');
      Assert.AreEqual('value2', LProps.GetValue<string>('key2'), 'props.key2');
    finally
      LJSON.Free;
    end;
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestDeserialization_StringDictionary;
begin
  var LObj := TJsonHelper.JSONToObject<TStrDictObj>(StrDictObjJSON);
  try
    Assert.AreEqual('TestLabel', LObj.Name, 'name');
    Assert.AreEqual(2, LObj.Props.Count, 'props count');
    Assert.AreEqual('value1', LObj.Props['key1'], 'props.key1');
    Assert.AreEqual('value2', LObj.Props['key2'], 'props.key2');
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestSerialization_ObjectDictionary;
begin
  var LObj := TObjDictObj.Create;
  try
    LObj.Name := 'DictLabel';
    var LChild1 := TItemObj.Create;
    LChild1.Value := 'v1';
    LChild1.Count := 10;
    LObj.Children.Add('child1', LChild1);
    var LChild2 := TItemObj.Create;
    LChild2.Value := 'v2';
    LChild2.Count := 20;
    LObj.Children.Add('child2', LChild2);
    var LJSON := TJsonHelper.ObjectToJSON(LObj);
    try
      Assert.AreEqual('DictLabel', LJSON.GetValue<string>('name'), 'name');
      var LChildren := LJSON.FindValue('children') as TJSONObject;
      Assert.IsNotNull(LChildren, 'children object present');
      Assert.AreEqual('v1', LChildren.FindValue('child1').GetValue<string>('value'), 'child1.value');
      Assert.AreEqual(10, LChildren.FindValue('child1').GetValue<Integer>('count'), 'child1.count');
      Assert.AreEqual('v2', LChildren.FindValue('child2').GetValue<string>('value'), 'child2.value');
      Assert.AreEqual(20, LChildren.FindValue('child2').GetValue<Integer>('count'), 'child2.count');
    finally
      LJSON.Free;
    end;
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestDeserialization_ObjectDictionary;
begin
  var LObj := TJsonHelper.JSONToObject<TObjDictObj>(ObjDictObjJSON);
  try
    Assert.AreEqual('DictLabel', LObj.Name, 'name');
    Assert.AreEqual(2, LObj.Children.Count, 'children count');
    Assert.AreEqual('v1', LObj.Children['child1'].Value, 'child1.value');
    Assert.AreEqual(10, LObj.Children['child1'].Count, 'child1.count');
    Assert.AreEqual('v2', LObj.Children['child2'].Value, 'child2.value');
    Assert.AreEqual(20, LObj.Children['child2'].Count, 'child2.count');
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestDeserialization_StringList_Empty;
begin
  var LObj := TJsonHelper.JSONToObject<TTaggedObj>(TaggedObjEmptyJSON);
  try
    Assert.AreEqual('TestName', LObj.Name, 'name');
    Assert.AreEqual(0, LObj.Tags.Count, 'tags count');
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestDeserialization_ClassicStringList_Empty;
begin
  var LObj := TJsonHelper.JSONToObject<TFlagObj>(FlaggedObjEmptyJSON);
  try
    Assert.AreEqual('TestName', LObj.Name, 'name');
    Assert.AreEqual(0, LObj.Flags.Count, 'flags count');
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestDeserialization_ObjectList_Empty;
begin
  var LObj := TJsonHelper.JSONToObject<TListObj>(ListObjEmptyJSON);
  try
    Assert.AreEqual('MyList', LObj.Title, 'title');
    Assert.AreEqual(0, LObj.Items.Count, 'items count');
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestDeserialization_StringDictionary_Empty;
begin
  var LObj := TJsonHelper.JSONToObject<TStrDictObj>(StrDictObjEmptyJSON);
  try
    Assert.AreEqual('TestLabel', LObj.Name, 'name');
    Assert.AreEqual(0, LObj.Props.Count, 'props count');
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestDeserialization_ObjectDictionary_Empty;
begin
  var LObj := TJsonHelper.JSONToObject<TObjDictObj>(ObjDictObjEmptyJSON);
  try
    Assert.AreEqual('DictLabel', LObj.Name, 'name');
    Assert.AreEqual(0, LObj.Children.Count, 'children count');
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestDeserialization_StringList_Missing;
begin
  var LObj := TJsonHelper.JSONToObject<TTaggedObj>(TaggedObjMissingJSON);
  try
    Assert.AreEqual('TestName', LObj.Name, 'name');
    Assert.AreEqual(0, LObj.Tags.Count, 'tags count');
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestDeserialization_ClassicStringList_Missing;
begin
  var LObj := TJsonHelper.JSONToObject<TFlagObj>(FlaggedObjMissingJSON);
  try
    Assert.AreEqual('TestName', LObj.Name, 'name');
    Assert.AreEqual(0, LObj.Flags.Count, 'flags count');
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestDeserialization_DynamicObject;
begin
  var LObj := TJsonHelper.JSONToObject<TDynObjEnvelop>('{"dynObj": {"name": "luca"}}');
  try
    Assert.IsNotNull(LObj.DynObj, 'DynObj propery is null');
    var LJSON := LObj.DynObj.ToJSON;
    try
      Assert.AreEqual('luca', LJSON.GetValue<string>('name'), 'dynObj.name with JSON');
      Assert.AreEqual('luca', LObj.DynObj.Name, 'dynObj.name with helper property');
    finally
      LJSON.Free;
    end;
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestDeserialization_ObjectList_Missing;
begin
  var LObj := TJsonHelper.JSONToObject<TListObj>(ListObjMissingJSON);
  try
    Assert.AreEqual('MyList', LObj.Title, 'title');
    Assert.AreEqual(0, LObj.Items.Count, 'items count');
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestDeserialization_StringDictionary_Missing;
begin
  var LObj := TJsonHelper.JSONToObject<TStrDictObj>(StrDictObjMissingJSON);
  try
    Assert.AreEqual('TestLabel', LObj.Name, 'name');
    Assert.AreEqual(0, LObj.Props.Count, 'props count');
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestDeserialization_ObjectDictionary_Missing;
begin
  var LObj := TJsonHelper.JSONToObject<TObjDictObj>(ObjDictObjMissingJSON);
  try
    Assert.AreEqual('DictLabel', LObj.Name, 'name');
    Assert.AreEqual(0, LObj.Children.Count, 'children count');
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestJSONObject_PreservesKeyOrderOnParse;
const
  LExpectedOrder: array [0 .. 4] of string = ('zebra', 'alpha', 'mike', 'bravo', 'delta');
begin
  // TJSONObject keeps its pairs in a TList, so iterating by index yields the
  // pairs in the exact order they appear in the source text.
  var LObj := TJSONObject.ParseJSONValue(OrderedKeysJSON) as TJSONObject;
  try
    Assert.IsNotNull(LObj, 'parsed object');
    Assert.AreEqual(Length(LExpectedOrder), LObj.Count, 'pair count');
    for var I := 0 to LObj.Count - 1 do
      Assert.AreEqual(LExpectedOrder[I], LObj.Pairs[I].JsonString.Value, Format('pair[%d]', [I]));
  finally
    LObj.Free;
  end;
end;

procedure TJSONTest.TestJSONObject_PreservesKeyOrderOnRoundTrip;
begin
  // Parsing then re-serializing must emit the keys in the same order, since
  // ToJSON walks the same internal list.
  var LObj := TJSONObject.ParseJSONValue(OrderedKeysJSON) as TJSONObject;
  try
    var LText := LObj.ToJSON;
    Assert.IsTrue(
      LText.IndexOf('zebra') < LText.IndexOf('alpha'), 'zebra before alpha');
    Assert.IsTrue(
      LText.IndexOf('alpha') < LText.IndexOf('mike'), 'alpha before mike');
    Assert.IsTrue(
      LText.IndexOf('mike') < LText.IndexOf('bravo'), 'mike before bravo');
    Assert.IsTrue(
      LText.IndexOf('bravo') < LText.IndexOf('delta'), 'bravo before delta');
  finally
    LObj.Free;
  end;
end;

{ TFlagObj }

{ TFlagObj }

constructor TFlagObj.Create;
begin
  FFlags := TStringList.Create;
end;

destructor TFlagObj.Destroy;
begin
  FFlags.Free;
  inherited;
end;

{ TDynObj }

constructor TDynObj.Create;
begin
  FJSON := TJSONNull.Create;
end;

destructor TDynObj.Destroy;
begin
  FJSON.Free;
  inherited;
end;

procedure TDynObj.FromJSON(AJSON: TJSONValue);
begin
  if Assigned(FJSON) then
    FJSON.Free;
  FJSON := AJSON.Clone as TJSONValue;
end;

function TDynObj.GetName: string;
begin
  Result := FJSON.GetValue<string>('name', '');
end;

function TDynObj.ToJSON: TJSONValue;
begin
  Result := FJSON.Clone as TJSONValue;
end;

{ TDynObjEnvelop }

constructor TDynObjEnvelop.Create;
begin
  FDynObj := TDynObj.Create;
end;

destructor TDynObjEnvelop.Destroy;
begin
  FDynObj.Free;
  inherited;
end;

initialization
  TDUnitX.RegisterTestFixture(TJSONTest);

end.
